# https://just.systems

set shell := ["nu", "-c"]

compiler := "clang"
generator := "ninja-multi"
build_type := "debug"

build_profile := compiler + "-" + generator + "-" + build_type

sources_dir := if path_exists("CMakeLists.txt") == "true" { "." } else { "sources" }
conan_dir := justfile_directory()

default: install configure build
rebuild *FLAGS: clean (install FLAGS) configure build

test TEST='.*': build
	ctest --preset conan-{{build_type}} --output-on-failure -R {{TEST}}

build TARGET='all': devenv
	cmake --build --preset conan-{{build_type}} -t {{TARGET}}

configure:
	cmake -S {{sources_dir}} --preset conan-default -DCMAKE_EXPORT_COMPILE_COMMANDS=ON

reconfigure:
	cmake -S {{sources_dir}} --preset conan-default -DCMAKE_EXPORT_COMPILE_COMMANDS=ON --fresh

install *FLAGS:
	conan install {{conan_dir}} -pr:a {{build_profile}} -b missing {{FLAGS}}

clean:
	git clean -xdf -e Cargo.lock -e .vscode -e sources -e justfile -e tools -e integration-tests

devenv:
	nu ([~/Projects .dotfiles cpp setup-cmake.nu] | path join)

create *FLAGS:
	conan create {{conan_dir}} --user cn --channel staging -pr:a {{build_profile}} -b missing {{FLAGS}}

recreate *FLAGS: cleancache (create FLAGS)

cleancache:
    conan remove {{file_name(conan_dir)}} -c
